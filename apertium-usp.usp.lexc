!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%<n%>                                  ! Sustantivo
%<np%>                                 ! Nombre propio
%<det%>                                ! Determinante
%<adj%>                                ! Adjetivo
%<num%>                                ! Numeral
%<adv%>                                ! Adverbio
%<pr%>                                 ! Preposición

%<cog%>                                ! Cognomen

%<ints%>                               ! Intensificador

%<perf%>                               ! Perfectivo (Completivo)
%<impf%>                               ! Imperfectivo (Incompletivo)

%<px1sg%>                              ! Posesivo de 1ra persona singular
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%{w%}                                  ! w / 0
%{n%}                                  ! n / w 
%{j%}                                  ! j / r 
%{í%}                                  ! í / 0 
%{a%}                                  ! a / 0
%{ː%}                                  ! a / e / i / o / u
%{y%}                                  ! 0
%{i%}                                  ! i / 0

@D.Poss@                               !
@P.Poss.No@                            !
@R.Poss.No@                            !
@P.Poss.Pl@                            !
@R.Poss.Pl@                            !

%>

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

                                       Miscellaneous ;
                                       Preposiciones ;
                                       Direccionales ;
                                       Prefijos ; 
@P.Poss.No@:@P.Poss.No@                Sustantivos ;
                                       Propios ; 
                                       Numerales ; 
                                       Determinantes ; 
                                       Pronombres ; 
                                       Adjetivos ; 
                                       Adverbios ; 
                                       Punctuación ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prefijos

Posesivo ;
Aspecto ;

LEXICON Posesivo 

!El prefijo de primera persona lleva tono cuando hace diferencia entre marcador de posesión y
!sujeto de predicado no verbal. Es decir, si en el primera ejemplo íntzʼiʼ ‘mi perroʼ se le quita el
!tono in tzʼiʼ se interpreta como ‘soy perroʼ, aunque el sujeto se escriba separado del predicado.

                                                                         !     _ Cns       !     _ Vow  
!%<px1sg%>:%{í%}%{n%}%{w%}%>                                  Nominales ; ! in- (ín-)¹, iin | w-, in- (inw-)
%<px1sg%>:%{í%}%{n%}%>                                       Nominales ; ! in- (ín-)       | w-
%<px2sg%>:a%{w%}%>                                           Nominales ; ! a-              | aw-
%<px3sg%>:%{j%}%>                                            Nominales ; ! j-              | r-
%<px1pl%>:q%{a%}%>                                           Nominales ; ! qa-             | q-
@P.Poss.Pl@%<px2pl%>:@P.Poss.Pl@a%{w%}%>                     Nominales ; ! a-          -aq | aw-       -aq
@P.Poss.Pl@%<px3pl%>:@P.Poss.Pl@%{j%}%>                      Nominales ; ! j-          -aq | r-        -aq

LEXICON Nominales 
                                                             Sustantivos ;
                                                             Poseídos ;
                                                             Relacionales ;

LEXICON Aspecto 

%<perf%>:x%>                                                 A-IV ; 
%<perf%>:x%>                                                 A-TV ; 
%<impf%>:t%{i%}%>                                            A-IV-3 ;    ! ti-, t-
%<impf%>:                                                    A-IV-3 ;
%<impf%>:                                                    A-IV-12 ;   ! ø-
%<impf%>:t%{i%}%>                                            A-TV-3 ; 
%<impf%>:                                                    A-TV-3 ;              ! Taʼ ø-ø-raj ti-ø-j-yeʼ r-ee.
%<impf%>:                                                    A-TV-12 ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON K

%+ch%<adv%>%<dir%>:%>ch                                      # ;
%+w%<adv%>%<dir%>:%>w                                        # ;
                                                             # ;

LEXICON SS-IV ! TODO: Flags

%+ik%<mark%>:%>ik                                            # ;
%+oq%<mark%>:%>oq                                            # ;

LEXICON SS-TV ! TODO: Flags

%+w%<mark%>:%>w                                              # ;
%+eʼ%<mark%>:%>eʼ                                            # ;

LEXICON Sufijos1

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON Sufijos2

@R.Poss.No@:@R.Poss.No@                                      # ;

LEXICON Sufijos3

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON A-IV-12

%<s_sg1%>:%{í%}%{n%}%>                                       IV ;
%<s_sg2%>:at%>                                               IV ;
%<s_pl1%>:oj%>                                               IV ;
%<s_pl2%>:at%>                                               IV ;

LEXICON A-IV-3 

%<s_sg3%>:                                                   IV ;
%<s_pl3%>:                                                   IV ;

LEXICON A-IV

                                                             A-IV-12 ;
                                                             A-IV-3 ;
LEXICON A-TV-12

%<o_sg1%>:in%>                                               B-TV ;
%<o_sg2%>:at%>                                               B-TV ;
%<o_pl1%>:oj%>                                               B-TV ;
%<o_pl2%>:at%>                                               B-TV ;

LEXICON A-TV-3 

%<o_sg3%>:                                                   B-TV ;
%<o_pl3%>:                                                   B-TV ;

LEXICON A-TV
                                                             A-TV-12 ;
                                                             A-IV-3 ;

LEXICON B-TV

%<s_sg1%>:in%{w%}%>                                          TV ;
%<s_sg2%>:a%{w%}%>                                           TV ;
%<s_sg3%>:%{j%}%>                                            TV ; 
%<s_pl1%>:q%{a%}%>                                           TV ;
%<s_pl2%>:a%{w%}%>                                           TV ;
%<s_pl3%>:                                                   TV ; 
%<s_pl3%>:%{j%}%>                                            TV ; 


LEXICON V-IV 

%<v%>%<iv%>:                                                 SS-IV ;
%<v%>%<iv%>:                                                 K ;

LEXICON V-TV 

%<v%>%<tv%>:                                                 SS-TV ;
%<v%>%<tv%>:                                                 K ;

LEXICON V-TV-D

%<v%>%<tv%>:%>j                                              SS-TV ;
%<v%>%<tv%>:%>j                                              K ;


LEXICON N1

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 

LEXICON N2

%<n%>:%>ij                                                   Sufijos2 ; 
%<n%>:%>aaj                                                  Sufijos3 ; 

LEXICON N3

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos2 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos1 ; 

LEXICON N4

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos2 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos1 ; 

LEXICON N5

%<n%>:%>niik                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N6

%<n%>:%>xeel                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N7

%<n%>:                                                       Sufijos3 ; 

LEXICON N8

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 

LEXICON N9

%<n%>:                                                       Sufijos2 ; 

LEXICON N10

%<n%>:                                                       Sufijos2 ; 
%<n%>:%>il                                                   Sufijos3 ; 


LEXICON REL

%<n%>%<rel%>:                                                Sufijos3 ; 

LEXICON A

%<adj%>:                                                     # ; 
%<adj%>%<ints%>:%>laj                                        # ; 

LEXICON NUM1

%<num%>:                                                     # ; 

LEXICON NUM2

%<num%>:                                                     # ; 

LEXICON COG1

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>aabʼ                                   # ;

LEXICON COG2

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>iibʼ                                   # ;

LEXICON COG3

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>ʼiibʼ                                  # ;

LEXICON COG4

%<np%>%<cog%>:                                               # ;
! TODO: Añadir plural

LEXICON ANTF

%<np%>%<ant%>%<f%>:                                          # ;

LEXICON ANTM

%<np%>%<ant%>%<m%>:                                          # ;

LEXICON TOP

%<np%>%<top%>:                                               # ;

LEXICON ADV1

%<adv%>:                                                     # ; 

LEXICON ADV2

%<adv%>%<itg%>:                                              # ; 

LEXICON DET1

%<det%>:                                                     # ; 

LEXICON DET2

%<det%>%<pl%>:                                               # ; 

LEXICON PRON2

%<prn%>%<itg%>:                                              # ; 

LEXICON PRON3

%<prn%>%<dem%>:                                              # ; 



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Miscellaneous

ra%<dim%>:ra                 # ;
tra%<dim%>:tra               # ;
aj%<gen%>:aj                 # ;
no%<neg%>:no                 # ;
taʼn%<neg%>:taʼn             # ;
taʼ%<neg%>:taʼ               # ;
ta%<neg%>:ta                 # ;
kitaʼ%<neg%>:kitaʼ           # ;
ma%<neg%>:ma                 # ;
ni%<neg%>:ni                 # ;

jinon%<ij%>:jinon            # ; 

tijin%<aux%>:tijin           # ;
tijin%<aux%>:tijiin          # ; ! Src/Covid

sebʼlaj%<ints%>:sebʼlaj      # ;
pok%<ints%>:pok              # ;
laj%<ints%>:laj              # ;


taq%<pl%>:taq                # ;
chaq%<pl%>:chaq              # ;
kʼis%<pl%>:kʼis              # ;

qaaj%<clf%>%<m%>:qaaj        # ;
chuch%<clf%>%<f%>:chuch      # ;

kwando%<cnjsub%>:kwando      # ; ! "cuando"
we%<cnjsub%>:we              # ; ! "si"
i%<cnjcoo%>:i                # ; ! "y"
per%<cnjcoo%>:per            # ; ! "pero"
o%<cnjcoo%>:o                # ; ! "o"
jwiʼl%<cnjsub%>:jwiʼl        # ; ! "porque"

jaʼ%<part%>:jaʼ              # ; ! "pues"

wi%<part%>:wi                # ; ! "PART" [#]
wiʼ%<part%>:wiʼ              # ; ! "PART" [#]
kʼu%<part%>:kʼu              # ; ! "PART" [#]


LEXICON Preposiciones 

chi%<pr%>:chi                # ;
li%<pr%>:li                  # ;
la%<pr%>:la                  # ;
laj%<pr%>:laj                # ;
laʼ%<pr%>:laʼ                # ;
pu%<pr%>:pu                  # ;


! Contracciones 

chi%<pr%>%+%<px1sg%>ech%<n%>%<rel%>:ch%>w%>e # ;  ! "" 
chi%<pr%>%+%<px2sg%>ech%<n%>%<rel%>:ch%>aw%>e # ;  ! "" 

LEXICON Pronombres

neen:neen                    PRON2 ; ! "qué"

riʼ:riʼ                      PRON3 ; ! "eso es" [#]
li:li                        PRON3 ; ! "" [#]

                             Pers ;

LEXICON Pers 

in%<prn%>%<pro%>%<p1%>%<sg%>:in # ;
at%<prn%>%<pro%>%<p2%>%<sg%>:at # ;
re%<prn%>%<pro%>%<p3%>%<sg%>:re # ;
oj%<prn%>%<pro%>%<p1%>%<pl%>:oj # ;
rechaq%<prn%>%<pro%>%<p3%>%<pl%>:rechaq # ;
ataq%<prn%>%<pro%>%<p2%>%<pl%>:ataq # ;
retaq%<prn%>%<pro%>%<p3%>%<pl%>:retaq # ;

iin%<prn%>%<pers%>%<p1%>%<sg%>:iin # ;
aat%<prn%>%<pers%>%<p2%>%<sg%>:aat # ;
re%<prn%>%<pers%>%<p3%>%<sg%>:re # ;
ooj%<prn%>%<pers%>%<p1%>%<pl%>:ooj # ;
rechaq%<prn%>%<pers%>%<p3%>%<pl%>:rechaq # ;
ataq%<prn%>%<pers%>%<p2%>%<pl%>:ataq # ;
retaq%<prn%>%<pers%>%<p3%>%<pl%>:retaq # ;

LEXICON Direccionales

kan%<adv%>%<dir%>:kan # ;
bʼi%<adv%>%<dir%>:bʼi # ;

LEXICON Determinantes 

li:li                        DET1 ; ! "el"
ri:ri                        DET1 ; ! "el"
man:man                      DET1 ; ! "el"
man:maq                      DET2 ; ! "el"
yaj:yaj                      DET1 ; ! "el"
yaj:yaq                      DET2 ; ! "el"

jun:jun                      DET1 ; ! "un"

LEXICON Numerales 

juun:juun                    NUM1 ; ! "uno" [#]
kiibʼ:kiibʼ                  NUM2 ; ! "dos" [#]
joobʼ:joobʼ                  NUM2 ; ! "cinco" [#]
lajuuj:lajuuj                NUM2 ; ! "diez" [#]

wunaq:wunaq                  NUM2 ; ! "veintena" [#]
kawunaq:kawunaq              NUM2 ; ! "cuarenta" [#]

LEXICON Sustantivos 

ichaaj:ich                   N2 ; ! "hierba" [#]
íxim:íxim                    N1 ; ! "maíz" [#]
óoj:óoj                      N1 ; ! "aguacate" [#]
aabʼ:aabʼ                    N1 ; ! "hamaca" [#]
tzʼiʼ:tzʼiʼ                  N1 ; ! "perro" [#]
uuq:uuq                      N1 ; ! "corte" [#]
tikoʼn:tikoʼn                N1 ; ! "siembra" [#]
ikʼlaal:ikʼlaal              N1 ; ! "hijo (de mujer)" [#]
awaj:awaj                    N3 ; ! "animal" [#]
ajchaak:aj%>chaak            N4 ; ! "trabajador" [aj#]
ajbʼiix:aj%>bʼiix            N4 ; ! "cantante" [aj#]
iyoom:iyoom                  N1 ; ! "comadrona" [#]
bʼee:bʼee                    N1 ; ! "camino" [#]
jal:ja%{ː%}l                 N1 ; ! "mazorca" [#]
kʼum:kʼu%{ː%}m               N1 ; ! "ayote" [#]
tiʼniik:tiʼ                  N5 ; ! "carne" [#niik]
qʼuniik:qʼu                  N5 ; ! "chamarra" [#niik]
jiʼxeel:ji%{ː%}ʼ             N6 ; ! "yerno" [#xeel]
alibʼxeel:alibʼ              N6 ; ! "nuera" [#xeel]
kʼojolxeel:kʼojol            N6 ; ! "joven" [#xeel]
qajawxeel:qajaw              N6 ; ! "dueño" [#xeel] ! FIXME: qa- poss. ?
qʼunxeel:qʼun                N6 ; ! "hermano menor" [#xeel]

tinmit:tinmi%{ː%}t           N1 ; ! "pueblo" [#]
yajeel:yajeel                N1 ; ! "enfermo" [#eel] ! FIXME: comprobar poss = jyaj ?
kʼeex:kʼeex                  N1 ; ! "trueque" [#]
kʼaat:kʼaat                  N1 ; ! "red" [#] ! FIXME: comprobar categoria
luwar:luwar                  N1 ; ! "lugar" [#]
luwar:luwr                   N1 ; ! "lugar" [#] ! Use/Spell ! Src/Covid
chubʼ:chubʼ                  N1 ; ! "saliba" [#]
atzʼaam:atzʼaam              N1 ; ! "sal" [#]
atzʼaam:atzaam               N1 ; ! "sal" [#]  ! Use/Spell ! Src/Covid
siikʼ:siikʼ                  N1 ; ! "cigarro" [#]
siikʼ:siik                   N1 ; ! "cigarro" [#] ! Use/Spell ! Src/Covid
chʼiichʼ:chʼiichʼ            N1 ; ! "vehículo" [#]
chʼiichʼ:chʼiich             N1 ; ! "vehículo" [#] ! Use/Spell ! Src/Covid
tataʼ:tataʼ                  N1 ; ! "señor" [#]
jaaʼ:jaaʼ                    N1 ; ! "mollera" [#]
niʼj:niʼj                    N1 ; ! "lagartija" [#]
yaaj:yaaj                    N1 ; ! "enfermo" [#]
usum:usum                    N1 ; ! "mosquito" [#]
teew:teew                    N1 ; ! "aire" [#]
wunaq:wunaq                  N1 ; ! "hombre" [#]
junabʼ:junabʼ                N1 ; ! "año" [#]
ánim¹:anim                   N1 ; ! "corazón" [#]
ánim²:ánim                   N1 ; ! "mujer" [#]
ánim²:ánm                    N1 ; ! "mujer" [#]
ákʼel:ákʼel                  N1 ; ! "niño" [#]
ékʼel:ékʼel                  N1 ; ! "niño" [#]
saqwech:saqwech              N1 ; ! "papa" [#]
ichijil:ichijil              N1 ; ! "esposo" [#]

cheeʼ¹:cheeʼ                 N1 ; ! "árbol" [#]
cheeʼ²:cheeʼ                 N1 ; ! "cárcel" [#]
kinaqʼ:kinaqʼ                N1 ; ! "fríjol" [#]
kolobʼ:kolobʼ                N1 ; ! "lazo" [#]
maam:maam                    N1 ; ! "abuelo" [#]
sokeʼl:sokeʼl                N1 ; ! "cortada" [#]
tuuj:tuuj                    N1 ; ! "temascal" [#]
kitz:kitz                    N1 ; ! "diente de leche" [#]
aqan:aqan                    N1 ; ! "pie" [#]
bʼoʼj:bʼoʼj                  N1 ; ! "olla" [#]

waa¹:waa                     N1 ; ! "tortilla" [#]
waa²:waa                     N1 ; ! "barros" [#]

pwaq:pwaq                    N1 ; ! "dinero" [#]
wuuj:wuuj                    N1 ; ! "libro" [#]
wuuj:wuj                     N1 ; ! "libro" [#] ! Use/Spell ! Src/Shoebox

aaq:aaq                      N1 ; ! "cerdo" [#]
abʼaj:abʼaj                  N1 ; ! "piedra" [#]
abʼiix:abʼiix                N1 ; ! "milpa" [#]
bʼa:bʼa                      N1 ; ! "cabeza" [#]
chaak:chaak                  N1 ; ! "trabajo" [#]
chiʼ:chiʼ                    N1 ; ! "boca" [#]
chʼool:chʼool                N1 ; ! "estomago" [#]
ekʼ:ekʼ                      N1 ; ! "pollo" [#]
jaʼ:jaʼ                      N1 ; ! "agua" [#]
kaʼn:kaʼn                    N1 ; ! "animal" [#]
kwaʼy:kwaʼy                  N1 ; ! "caballo" [#]
laq:laq                      N1 ; ! "escudilla" [#]
mex:mex                      N1 ; ! "izquierda" [#]
neʼ:neʼ                      N1 ; ! "nene" [#]
pootʼ:pootʼ                  N1 ; ! "güipil" [#]
qeleen:qeleen                N1 ; ! "cosa" [#]
qʼabʼ:qʼabʼ                  N1 ; ! "mano" [#]
raʼl:aʼl                     N1 ; ! "jugo" [r#]
siiʼ:siiʼ                    N1 ; ! "leña" [#]
tziij:tziij                  N1 ; ! "palabra" [#]
tzʼolom:tzʼolom              N1 ; ! "tabla" [#]
uleew:uleew                  N1 ; ! "hígado" [#]
wakix:wakix                  N1 ; ! "vaca.toro" [#]
witz:witz                    N1 ; ! "cerro" [#]
woron:woron                  N1 ; ! "sacate" [#]
xajabʼ:xajabʼ                N1 ; ! "zapato" [#]
xaq:xaq                      N1 ; ! "sarro" [#]

chikech:chikech              N1 ; ! "canasto" [#]
chikʼokʼ:chikʼokʼ            N1 ; ! "tostada" [#]
chʼiim:chʼiim                N1 ; ! "güisquil" [#]
iik:iik                      N1 ; ! "chile" [#]
imul:imul                    N1 ; ! "conejo" [#]
joʼchʼ:joʼchʼ                N1 ; ! "atol.masa" [#]
jul:jul                      N1 ; ! "hoyo" [#]
kaabʼ:kaabʼ                  N1 ; ! "dulce, miel, panela" [#]
kʼim:kʼim                    N1 ; ! "paja" [#]
pech:pech                    N1 ; ! "compañero" [#]
qul:qul                      N1 ; ! "voz" [#]
qʼabʼreel:qʼabʼreel          N1 ; ! "bolo" [#]
qʼooqʼ:qʼooqʼ                N1 ; ! "chilacayote" [#]
qʼoor:qʼoor                  N1 ; ! "masa de maíz" [#]
sipoʼl:sipoʼl                N1 ; ! "hinchazón" [#]
traas:traas                  N1 ; ! "durazno" [#]
tuul:tuul                    N1 ; ! "banano" [#]
tzi:tzi                      N1 ; ! "nixtamal" [#]
xaan:xaan                    N1 ; ! "adobe" [#]
suutzʼ:suutzʼ                N1 ; ! "nube" [#]
kʼexkʼoliil:kʼexkʼoliil      N1 ; ! "dolor" [#]

xiil:xiil                    N1 ; ! "silla" [#]
pipi:pipi                    N1 ; ! "gallito arbórea" [#]
moor:moor                    N1 ; ! "machete" [#]
mees:mees                    N1 ; ! "basura" [#]
kʼuch:kʼuch                  N1 ; ! "zopilote" [#]
kʼotobʼ:kʼotobʼ              N1 ; ! "poza" [#]
kʼooy:kʼooy                  N1 ; ! "mico" [#]
juul:juul                    N1 ; ! "hoyo" [#]
jaabʼ:jaabʼ                  N1 ; ! "lluvia" [#]
chʼoʼj:chʼoʼj                N1 ; ! "pelea" [#]
chaaj:chaaj                  N1 ; ! "ceniza" [#]
bʼaaq:bʼaaq                  N1 ; ! "hueso" [#]
bʼaa:bʼaa                    N1 ; ! "taltuza" [#]

wa:wa                        N1 ; ! "tortilla" [#]
tzam:tzam                    N1 ; ! "nariz" [#]
toqʼobʼ:toqʼobʼ              N1 ; ! "favor" [#]
tiitʼ:tiitʼ                  N1 ; ! "abuela" [#]
qʼiibʼ:qʼiibʼ                N1 ; ! "cántaro" [#]
poop:poop                    N1 ; ! "petate" [#]
paar:paar                    N1 ; ! "zorro" [#]
noʼj:noʼj                    N1 ; ! "conocimiento" [#]
mop:mop                      N1 ; ! "coyol" [#]

maqʼnaʼ:maqʼnaʼ              N1 ; ! "agua caliente" [#]
lool:lool                    N1 ; ! "grillo" [#]
kʼux:kʼux                    N1 ; ! "elote" [#]
kʼisis:kʼisis                N1 ; ! "ciprés" [#]
kirtyaan:kirtyaan            N1 ; ! "persona" [#]
kapee:kapee                  N1 ; ! "cafe" [#]
joochʼ:joochʼ                N1 ; ! "atol" [#]
ixpaqar:ixpaqar              N1 ; ! "sapo" [#]
eekʼ:eekʼ                    N1 ; ! "pie de gallo" [#]

ee:ee                        N1 ; ! "diente" [#]
chʼim:chʼim                  N1 ; ! "güisquil" [#]
bʼolaʼj:bʼolaʼj              N1 ; ! "tun, trozos" [#]
bʼaatzʼ:bʼaatzʼ              N1 ; ! "saraguate" [#]
bʼaatzʼ:bʼaatzʼ              N1 ; ! "hilo" [#]

qʼaqʼaal:qʼaqʼaal            N1 ; ! "temperatura" [#]

kúukʼ:kúukʼ                  N1 ; ! "ardilla" [#]
rexkuukʼ:rexkuukʼ            N1 ; ! "ardilla" [rex#]
tzʼaʼrkúʼkʼ:tzʼaʼrkúʼkʼ      N1 ; ! "ardilla" [tzʼaʼr#]

jekʼon:jekʼon                N1 ; ! "jalón" [#]

kristyaan:kristyaan          N1 ; ! "cristiano" [#] 
kristyaan:kristyan           N1 ; ! "cristiano" [#]  ! Use/Spell ! Src/Covid

kʼutubʼ:kʼutubʼ              N8 ; ! "medidor" [#ubʼ]

kʼexkʼol:kʼexkʼol            N1 ; ! "dolor" [#]

jaa:jaa                      N9 ; ! "casa" [#]

ixóqil:ixóq                  N10 ; ! "hembra"

qʼiij:qʼiij                  N1 ; ! "día" [#]


LEXICON Poseídos 

iij:iij                      N7 ; ! "espalda" [-#]
chooch:%{y%}chooch           N7 ; ! "casa" [-#]
bʼiij:bʼiij                  N7 ; ! "nombre" [-#]
bʼiij:bʼij                   N7 ; ! "nombre" [-#] ! Use/Spell ! Src/Shoebox

LEXICON Relacionales

ech:e%{ː%}ch                 REL ; ! "de (pertenencia)"
wach:wach                    REL ; ! "enfrente de"
iij:iij                      REL ; ! "atrás de"
iij:ij                       REL ; ! "atrás de" ! Use/Spell ! Src/Shoebox
elaaj:elaaj                  REL ; ! "debajo de"
qul:qul                      REL ; ! "cuello"
bʼaa:bʼaa                    REL ; ! "sobre de"
achaq:achaq                  REL ; ! "?"
xúkut:xúkut                  REL ; ! "al lado de"
ikʼil:ikʼil                  REL ; ! "?"
chiʼ:chiʼ                    REL ; ! "orilla de"
iibʼ:iibʼ                    REL ; ! "" 
iibʼ:ibʼ                     REL ; ! ""  ! Use/Spell ! Src/Shoebox
wiʼl:wiʼl                    REL ; ! ""
paam:paam                    REL ; ! "fondo"
wiich:wiich                  REL ; ! ""
wiich:wich                   REL ; ! "" ! Use/Spell ! Src/Shoebox
ee:ee                        REL ; ! ""

! FIXME: Sólo p3
!peech
!laʼ

LEXICON Propios 

Chimel:Chimel                TOP ; 
Kʼaʼntun:Kʼaʼntun            TOP ; ! ""
Kʼotoxaq:Kʼotoxaq            TOP ; ! ""
Maqaljaʼ:Maqaljaʼ            TOP ; ! ""
Bʼoobʼlaj:Bʼoobʼlaj          TOP ; ! ""
Kʼisachálik:Kʼisachálik      TOP ; ! ""
Najtilja:Najtilja            TOP ; ! ""
Chʼipaq:Chʼipaq              TOP ; ! ""
Kʼixpaaj:Kʼixpaaj            TOP ; ! ""

Chʼolaʼ:Chʼolaʼ              TOP ; ! "Cholá" [#]
Najtijaa:Najtijaa            TOP ; ! "?" [#]
Kʼisachaal:Kʼisachaal        TOP ; ! "Quisachal" [#]
Juʼjoloom:Juʼjoloom          TOP ; ! "?" [#]
Jumuq:Jumuq                  TOP ; ! "Jumuc" [#]
Chʼanuuj:Chʼanuuj            TOP ; ! "Chanuj" [#]
Chʼámaqʼ:Chʼámaqʼ            TOP ; ! "Chamác" [#]
Chʼaamqʼ:Chʼaamqʼ            TOP ; ! "Chamác" [#]
Chʼaaj:Chʼaaj                TOP ; ! "Chaj" [#]
Choʼkoox:Choʼkoox            TOP ; ! "?" [#]
Chimeel:Chimeel              TOP ; ! "Chimel" [#]


Koʼr:Koʼr                    COG1 ; 
Tum:Tum                      COG2 ;
Aware:Aware                  COG3 ; 

Tok:Tok                      COG4 ; ! "Toc" [#]
Nuul:Nuul                    COG4 ; ! "Pinula" [#]
Koj:Koj                      COG4 ; ! "Coj" [#]
Chʼiit:Chʼiit                COG4 ; ! "Chitop" [#]
Xooy:Xooy                    COG4 ; ! "apellido" [#]
Rawix:Rawix                  COG4 ; ! "Ragüix" [#]
Puu:Puu                      COG4 ; ! "Pu" [#]
Pool:Pool                    COG4 ; ! "Pol" [#]
Pakay:Pacay                  COG4 ; ! "Pacay" [Pakay]
Paar:Paar                    COG4 ; ! "Par" [#]
Menchuʼ:Menchuʼ              COG4 ; ! "Menchu" [#]
Lux:Lux                      COG4 ; ! "Lux" [#]
Kʼibʼ:Kʼibʼ                  COG4 ; ! "Quip" [#]
Kaant:Kaant                  COG4 ; ! "Canto" [#]
Jaax:Jaax                    COG4 ; ! "Jax" [#]
Ixkotoʼy:Ixkotoʼy            COG4 ; ! "Ixcotoyac" [#]
Ixchoobʼ:Ixchoobʼ            COG4 ; ! "Ixchop" [#]
Imul:Imul                    COG4 ; ! "Imul" [#]
Ikʼal:Ikʼal                  COG4 ; ! "Ical" [#]
Ibʼooy:Ibʼooy                COG4 ; ! "Iboy" [#]
Estyuʼl:Estyuʼl              COG4 ; ! "Estayul" [#]
Chʼiik:Chʼiik                COG4 ; ! "Chic" [#]
Chʼaʼchʼ:Chʼaʼchʼ            COG4 ; ! "Chach" [#]
Chokooj:Chokooj              COG4 ; ! "Chocoj" [#]
Chitop:Chitop                COG4 ; ! "Chitop" [#]
Chaaj:Chaaj                  COG4 ; ! "Chaj" [#]
Ajpiʼ:Ajpiʼ                  COG4 ; ! "Ajpi" [#]
Ajiyataas:Ajiyataas          COG4 ; ! "Ajiatas" [#]
Ajchiku:Ajchiku              COG4 ; ! "Ajchicu" [#]
Ajaneel:Ajaneel              COG4 ; ! "Ajanel" [#]
Aaj:Aaj                      COG4 ; ! "Aj" [#]


Treʼs:Treʼs                  ANTF ; ! "Teresa" [#]
Terees:Terees                ANTF ; ! "Teresa" [#]
Tzʼúnun:Tzʼúnun              ANTF ; ! "" [#]

Liip:Liip                    ANTM ; ! "Felipe" [#]

Liin:Liin                    ANTF ; ! "Catarina" [#]
Paal:Paal                    ANTM ; ! "Gaspar" [#]
Leen:Leen                    ANTF ; ! "Elena" [#]
Laʼs:Laʼs                    ANTM ; ! "Francisco" [#]


LEXICON Adjetivos 

saq:saq                      A ; ! "blanco"
rex:rex                      A ; ! "verde"
keq:keq                      A ; ! "rojo"
qʼeq:qʼeq                    A ; ! "negro"
qʼan:qʼan                    A ; ! "amarillo"

chʼam:chʼam                  A ; ! "ácido"
kiʼ:kiʼ                      A ; ! "dulce"
kʼaa:kʼaa                    A ; ! "amargo"
tzaa:tzaa                    A ; ! "salado"
rii:rii                      A ; ! "picante"

nim:nim                      A ; ! "grande"
chaal:chaal                  A ; ! "pequeño"
nuuchʼ:nuuchʼ                A ; ! "pequeño"
maakʼ:maakʼ                  A ; ! "pequeño"

bʼot:bʼot                    A ; ! "blanco" < algodón
pool:pool                    A ; ! "blanco" < flor

aakʼ:aakʼ                    A ; ! "nuevo" [#]
looqʼ:looqʼ                  A ; ! "querido" [#]
qus:qus                      A ; ! "bueno" [#]
jechʼ:jechʼ                  A ; ! "torcido"
kʼis:kʼis                    A ; ! "ahumado" [#]
kʼojol:kʼojol                A ; ! "joven" [#]
loqʼlaj:loqʼlaj              A ; ! "santo" [#]
kʼaʼn:kʼaʼn                  A ; ! "enojado" [#]

joron:joron                  A ; ! "frío" [#]
tʼebʼ:tʼebʼ                  A ; ! "grueso" [#]
tzʼiil:tzʼiil                A ; ! "sucio" [#]
paatzʼ:paatzʼ                A ; ! "despeinado" [#]

sikʼ:sikʼ                    A ; ! "AFE" [#]
tzikʼ:tzikʼ                  A ; ! "AFE" [#]

kʼex:kʼex                    A ; ! "dolor" [#] ! TODO: Comprobar cat.

rajwéxik:rajwéxik            A ; ! "necesario" [#]


LEXICON Adverbios 

lamaas:lamaas                ADV2 ; ! "dónde"
lamaas:lamas                 ADV2 ; ! "dónde" ! Src/Shoebox

chweʼq:chweʼq                ADV1 ; ! "mañana"
íwir:íwir                    ADV1 ; ! "ayer"
lajori:lajori                ADV1 ; ! "ahora" [#]
jor:jor                      ADV1 ; ! "ahora" [#]
mir:mir                      ADV1 ; ! "hace un momento" [#]
lajor:lajor                  ADV1 ; ! "rápido" [#]
chik:chik                    ADV1 ; ! "PART" [#]

niri:niri                    ADV1 ; ! "aqui" [#]
ikim:ikim                    ADV1 ; ! "abajo" [#]
jiʼ:jiʼ                      ADV1 ; ! "allá" [#]
jili:jili                    ADV1 ; ! "allá" [#]
chaʼ:chaʼ                    ADV1 ; ! "neg." [#]
naj:naj                      ADV1 ; ! "lejos" [#]

qe:qe                        ADV1 ; ! "solo" [#]
ójor:ójor                    ADV1 ; ! "antiguamente" [#]
klaʼ:klaʼ                    ADV1 ; ! "allí" [#]
ajsik:ajsik                  ADV1 ; ! "arriba" [#]

klaʼ:klaʼ                    ADV1 ; ! "allí" [#]
kilaʼ:kilaʼ                  ADV1 ; ! "allí" [#]
nojoʼ:nojo                   ADV1 ; ! "por allí" [#ʼ]

na:na                        ADV1 ; ! "todavía" [#]



LEXICON TV

:%> TVʼ ;

LEXICON IV

:%> IVʼ ;

LEXICON IVʼ

war:war                      V-IV ; ! "dormir"
wiʼn:wiʼn                    V-IV ; ! "comer"
oqʼ:oqʼ                      V-IV ; ! "llorar"
taw:taw                      V-IV ; ! "llegar allá"
cheʼ:cheʼ                    V-IV ; ! "decir" [#]
peʼ:peʼ                      V-IV ; ! "venir"

almaj:almaj                  V-IV ; ! "escaparse" [#iik]
bʼen:bʼen                    V-IV ; ! "caminar" [#iik]
jol:jol                      V-IV ; ! "correr" [#iik]
kam:kam                      V-IV ; ! "morir" [#iik]
kan:kan                      V-IV ; ! "quedar" [#iik]
pax:pax                      V-IV ; ! "estallar" [#iik]
pet:pet                      V-IV ; ! "venir" [#iik]

bʼee:bʼee                    V-IV ; ! "ir" [#]
bʼooq:bʼooq                  V-IV ; ! "ir" [#]
chaʼ:chaʼ                    V-IV ; ! "decir" [#]
chiliil:chiliil              V-IV ; ! "desgranable" [#]
jasjan:jasjan                V-IV ; ! "secretiar" [#]
jat:jat                      V-IV ; ! "ir" [#]
joʼ:joʼ                      V-IV ; ! "ir" [#]
kaʼy:kaʼy                    V-IV ; ! "pretender" [#]
loqár:loqár                  V-IV ; ! "marchitarse" [#]
metʼeʼtʼ:metʼeʼtʼ            V-IV ; ! "tartamudear" [#]
qij:qij                      V-IV ; ! "dormir" [#]
sepeetzʼ:sepeetzʼ            V-IV ; ! "secar" [#]
xiʼ:xiʼ                      V-IV ; ! "ir" [#]

almaj:almaj                  V-IV ; ! "escaparse" [#]
bʼen:bʼen                    V-IV ; ! "caminar" [#]
chaqʼ:chaqʼ                  V-IV ; ! "madurar" [#]
ikʼaj:ikʼaj                  V-IV ; ! "sentir" [#]
jaʼ:jaʼ                      V-IV ; ! "agua" [#]
jer:jer                      V-IV ; ! "desgastarse" [#]
jol:jol                      V-IV ; ! "correr" [#]
kochʼ:kochʼ                  V-IV ; ! "aguantar" [#]
kos:kos                      V-IV ; ! "cansar" [#]
kʼabʼ:kʼabʼ                  V-IV ; ! "escurrir" [#]
kʼat:kʼat                    V-IV ; ! "quemarse" [#]
kʼeqet:kʼeqet                V-IV ; ! "comezón" [#]
kʼis:kʼis                    V-IV ; ! "terminarse" [#]
kʼiy:kʼiy                    V-IV ; ! "crecer" [#]
kʼiy:kʼiy                    V-IV ; ! "abundar" [#]
kʼutun:kʼutun                V-IV ; ! "aparecerse" [#]
loʼ:loʼ                      V-IV ; ! "comida" [#]
loʼw:loʼw                    V-IV ; ! "comer" [#]
maj:maj                      V-IV ; ! "comenzar" [#]
motʼ:motʼ                    V-IV ; ! "encoger" [#]
oqʼ:oqʼ                      V-IV ; ! "llorar" [#]
pax:pax                      V-IV ; ! "estallar" [#]
pet:pet                      V-IV ; ! "venir" [#]
qej:qej                      V-IV ; ! "bajar, llegar" [#]
qʼabʼar:qʼabʼar              V-IV ; ! "emborracharse" [#]
qʼobʼ:qʼobʼ                  V-IV ; ! "engordar" [#]
qʼulul:qʼulul                V-IV ; ! "tronar.estómago" [#]
qʼuy:qʼuy                    V-IV ; ! "frotar" [#]
tuqur:tuqur                  V-IV ; ! "débilitarse" [#]
um:um                        V-IV ; ! "retener agua en la boca" [#]
woʼkot:woʼkot                V-IV ; ! "pasear" [#]

ikʼaj:ikʼaj                  V-IV ; ! "sentir" [#]

LEXICON TVʼ

aj:aj                        V-TV ; ! "querer"

chap:chap                    V-TV ; ! "agarrar"
kʼam:kʼam                    V-TV ; ! "recibir"
il:il                        V-TV ; ! "ver"
tukʼ:tukʼ                    V-TV ; ! "empujar"
kʼut:kʼut                    V-TV ; ! "enseñar"
loqʼ:loqʼ                    V-TV ; ! "comprar"
tik:tik                      V-TV ; ! "sembrar"
bʼay:bʼay                    V-TV ; ! "entrener/demorar" 
cheqʼ:cheqʼ                  V-TV ; ! "tropezar"
bʼan:bʼan                    V-TV ; ! "hacer" [#]

qej:qej                      V-TV ; ! "prestar, alquilar, bajar, llegar" [#iik]
kʼat:kʼat                    V-TV ; ! "quemar, quemarse" [#iik]
maj:maaj                     V-TV ; ! "quitar, comenzar, masajear" [#iik]
maj:maj                      V-TV ; ! "quitar, comenzar, masajear" [#iik]

bʼit:bʼit                    V-TV ; ! "levantar" [#iik]
chap:chap                    V-TV ; ! "agarrar" [#iik]
cheqʼ:cheqʼ                  V-TV ; ! "tropezar" [#iik]
chup:chup                    V-TV ; ! "apagar" [#iik]
chʼebʼ:chʼebʼ                V-TV ; ! "conocer" [#iik]
chʼobʼ:chʼobʼ                V-TV ; ! "conocer" [#iik]
jor:jor                      V-TV ; ! "perforar" [#iik]
kachʼ:kachʼ                  V-TV ; ! "morder" [#iik]
kut:kut                      V-TV ; ! "disparar" [#iik]
kʼam:kʼam                    V-TV ; ! "recibir, guiar, juntar" [#iik]
kʼat:kʼat                    V-TV ; ! "quemar" [#iik]
kʼex:kʼex                    V-TV ; ! "cambiar" [#iik]
kʼul:kʼul                    V-TV ; ! "recibir" [#iik]
kʼur:kʼur                    V-TV ; ! "cortar" [#iik]
mol:mol                      V-TV ; ! "juntar, recoger" [#iik]
qʼatz:qʼatz                  V-TV ; ! "cortar con los dientes" [#iik]
qʼax:qʼax                    V-TV ; ! "pasar" [#iik]
qʼip:qʼip                    V-TV ; ! "quebrar" [#iik]
rak:rak                      V-TV ; ! "raspar" [#iik]
rep:rep                      V-TV ; ! "alumbrar" [#iik]
sok:sok                      V-TV ; ! "cortarlo" [#iik]
taʼ:taʼ                      V-TV ; ! "encontrar, preguntar, alcanzar" [#iik]
titzʼ:titzʼ                  V-TV ; ! "meter" [#iik]
tix:tix                      V-TV ; ! "regar" [#iik]
tok:tok                      V-TV ; ! "buscarlo" [#iik]
tʼok:tʼok                    V-TV ; ! "apretar" [#iik]
yeʼ:yeʼ                      V-TV ; ! "dar" [#iik]
yuq:yuq                      V-TV ; ! "estirar" [#iik]


qʼatatej:qʼatate             V-TV-D ; ! "cortar" [#j]
kamsáj:kamsá                 V-TV-D ; ! "matar" [#j]
komsáj:komsá                 V-TV-D ; ! "matar" [#j]

LEXICON Punctuación

.%<sent%>:. # ;
..%<sent%>:.. # ;
...%<sent%>:... # ;
%;%<sent%>:%; # ;
%:%<sent%>:%: # ;
%!%<sent%>:%! # ;
%¡%<sent%>:%¡ # ;
%-%<guio%>:%- # ;
%—%<guio%>:%— # ;
%–%<guio%>:%– # ;
,%<cm%>:, # ;
%?%<sent%>:%? # ;
%¿%<lquest%>:%¿ # ;
%"%<sent%>:%" # ;
%«%<lquot%>:%« # ;
%»%<rquot%>:%» # ;
%”%<rquot%>:%” # ;
%“%<lquot%>:%“ # ;
%(%<lpar%>:%( # ;
（%<lpar%>:（ # ;
%]%<rpar%>:%] # ;
%[%<lpar%>:%[ # ;
%)%<rpar%>:%) # ;
）%<rpar%>:） # ;
\%<sent%>:\ # ;
/%<sent%>:/ # ;
…%<sent%>:… # ;

