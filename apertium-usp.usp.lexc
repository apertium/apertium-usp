!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%<n%>                                  ! Sustantivo


%<px1sg%>                              ! Posesivo de 1ra persona singular
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%{w%}
%{n%}
%{j%}
%{í%}
%{a%}
%{ː%}

@D.Poss@                               !
@P.Poss.No@                            !
@R.Poss.No@                            !
@P.Poss.Pl@                            !
@R.Poss.Pl@                            !

%>

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

                                       Prefijos ; 
                                       Propios ; 
@P.Poss.No@:@P.Poss.No@                Sustantivos ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prefijos

Posesivo ;

LEXICON Posesivo 

!El prefijo de primera persona lleva tono cuando hace diferencia entre marcador de posesión y
!sujeto de predicado no verbal. Es decir, si en el primera ejemplo íntzʼiʼ ‘mi perroʼ se le quita el
!tono in tzʼiʼ se interpreta como ‘soy perroʼ, aunque el sujeto se escriba separado del predicado.

                                                                         !     _ Cns       !     _ Vow  
!%<px1sg%>:%{í%}%{n%}%{w%}%>                                  Nominales ; ! in- (ín-)¹, iin | w-, in- (inw-)
%<px1sg%>:%{í%}%{n%}%>                                       Nominales ; ! in- (ín-)       | w-
%<px2sg%>:a%{w%}%>                                           Nominales ; ! a-              | aw-
%<px3sg%>:%{j%}%>                                            Nominales ; ! j-              | r-
%<px1pl%>:q%{a%}%>                                           Nominales ; ! qa-             | q-
@P.Poss.Pl@%<px2pl%>:@P.Poss.Pl@a%{w%}%>                     Nominales ; ! a-          -aq | aw-       -aq
@P.Poss.Pl@%<px3pl%>:@P.Poss.Pl@%{j%}%>                      Nominales ; ! j-          -aq | r-        -aq

LEXICON Nominales 
                                                             Sustantivos ;
                                                             Poseídos ;

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Sufijos1

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON Sufijos2

@R.Poss.No@:@R.Poss.No@                                      # ;

LEXICON Sufijos3

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON N1

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 

LEXICON N2

%<n%>:%>ij                                                   Sufijos2 ; 
%<n%>:%>aaj                                                  Sufijos3 ; 

LEXICON N3

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos2 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos1 ; 

LEXICON N4

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos2 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos1 ; 

LEXICON N5

%<n%>:%>niik                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N6

%<n%>:%>xeel                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N7

%<n%>:                                                       Sufijos3 ; 

LEXICON COG1

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>aabʼ                                   # ;

LEXICON COG2

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>iibʼ                                   # ;

LEXICON COG3

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>ʼiibʼ                                  # ;



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON Sustantivos 

ichaaj:ich                   N2 ; ! "hierba" [#]
íxim:íxim                    N1 ; ! "maíz" [#]
óoj:óoj                      N1 ; ! "aguacate" [#]
aabʼ:aabʼ                    N1 ; ! "hamaca" [#]
tzʼiʼ:tzʼiʼ                  N1 ; ! "perro" [#]
uuq:uuq                      N1 ; ! "corte" [#]
tikoʼn:tikoʼn                N1 ; ! "siembra" [#]
ikʼlaal:ikʼlaal              N1 ; ! "hijo (de mujer)" [#]
awaj:awaj                    N3 ; ! "animal" [#]
ajchaak:aj%>chaak            N4 ; ! "trabajador" [aj#]
iyoom:iyoom                  N1 ; ! "comadrona" [#]
bʼee:bʼee                    N1 ; ! "camino" [#]
jal:ja%{ː%}l                 N1 ; ! "mazorca" [#]
kʼum:kʼu%{ː%}m               N1 ; ! "ayote" [#]
tiʼniik:tiʼ                  N5 ; ! "carne" [#niik]
qʼuniik:qʼu                  N5 ; ! "chamarra" [#niik]
jiʼxeel:ji%{ː%}ʼ             N6 ; ! "yerno" [#xeel]
alibʼxeel:alibʼ              N6 ; ! "nuera" [#xeel]

LEXICON Poseídos 

iij:iij                      N7 ; ! "espalda" [-#]
ichooch:ichooch              N7 ; ! "casa" [-#]

LEXICON Propios 

Koʼr:Koʼr                    COG1 ; 
Tum:Tum                      COG2 ;
Aware:Aware                  COG3 ; 

