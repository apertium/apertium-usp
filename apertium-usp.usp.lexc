!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
Multichar_Symbols

%<n%>                                  ! Sustantivo
%<np%>                                 ! Nombre propio
%<det%>                                ! Determinante
%<adj%>                                ! Adjetivo
%<num%>                                ! Numeral
%<adv%>                                ! Adverbio

%<cog%>                                ! Cognomen

%<ints%>                               ! Intensificador

%<px1sg%>                              ! Posesivo de 1ra persona singular
%<px2sg%>
%<px3sg%>
%<px1pl%>
%<px2pl%>
%<px3pl%>

%{w%}                                  ! w / 0
%{n%}                                  ! n / w 
%{j%}                                  ! j / r 
%{í%}                                  ! í / 0 
%{a%}                                  ! a / 0
%{ː%}                                  ! a / e / i / o / u

@D.Poss@                               !
@P.Poss.No@                            !
@R.Poss.No@                            !
@P.Poss.Pl@                            !
@R.Poss.Pl@                            !

%>

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Root

                                       Miscellaneous ;
                                       Prefijos ; 
@P.Poss.No@:@P.Poss.No@                Sustantivos ;
                                       Propios ; 
                                       Numerales ; 
                                       Determinantes ; 
                                       Pronombres ; 
                                       Adjetivos ; 
                                       Adverbios ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Prefijos

Posesivo ;
Aspecto ;

LEXICON Posesivo 

!El prefijo de primera persona lleva tono cuando hace diferencia entre marcador de posesión y
!sujeto de predicado no verbal. Es decir, si en el primera ejemplo íntzʼiʼ ‘mi perroʼ se le quita el
!tono in tzʼiʼ se interpreta como ‘soy perroʼ, aunque el sujeto se escriba separado del predicado.

                                                                         !     _ Cns       !     _ Vow  
!%<px1sg%>:%{í%}%{n%}%{w%}%>                                  Nominales ; ! in- (ín-)¹, iin | w-, in- (inw-)
%<px1sg%>:%{í%}%{n%}%>                                       Nominales ; ! in- (ín-)       | w-
%<px2sg%>:a%{w%}%>                                           Nominales ; ! a-              | aw-
%<px3sg%>:%{j%}%>                                            Nominales ; ! j-              | r-
%<px1pl%>:q%{a%}%>                                           Nominales ; ! qa-             | q-
@P.Poss.Pl@%<px2pl%>:@P.Poss.Pl@a%{w%}%>                     Nominales ; ! a-          -aq | aw-       -aq
@P.Poss.Pl@%<px3pl%>:@P.Poss.Pl@%{j%}%>                      Nominales ; ! j-          -aq | r-        -aq

LEXICON Nominales 
                                                             Sustantivos ;
                                                             Poseídos ;

LEXICON Aspecto 

%<perf%>:x%>                                                 A-IV ; 
%<perf%>:x%>                                                 A-TV ; 

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!


LEXICON Sufijos1

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON Sufijos2

@R.Poss.No@:@R.Poss.No@                                      # ;

LEXICON Sufijos3

@D.Poss@:@D.Poss@                                            # ;
@R.Poss.Pl@:@R.Poss.Pl@%>aq                                  # ;

LEXICON A-IV 

%<s_sg1%>:in%>                                               IV ;
%<s_sg2%>:at%>                                               IV ;
%<s_sg3%>:                                                   IV ;
%<s_sg1%>:oj%>                                               IV ;
%<s_sg2%>:at%>                                               IV ;
%<s_sg3%>:                                                   IV ;

LEXICON A-TV 

%<o_sg1%>:in%>                                               B-TV ;
%<o_sg2%>:at%>                                               B-TV ;
%<o_sg3%>:                                                   B-TV ;
%<o_pl1%>:oj%>                                               B-TV ;
%<o_pl2%>:at%>                                               B-TV ;
%<o_pl3%>:                                                   B-TV ;

LEXICON B-TV

%<s_sg1%>:in%{w%}%>                                          TV ;
%<s_sg2%>:a%{w%}%>                                           TV ;
%<s_sg3%>:%{j%}%>                                            TV ; 
%<s_pl1%>:q%{a%}%>                                           TV ;
%<s_pl2%>:a%{w%}%>                                           TV ;
%<s_pl3%>:                                                   TV ; 
%<s_pl3%>:%{j%}%>                                            TV ; 


LEXICON V-IV 

%<v%>%<iv%>:                                                 # ;

LEXICON V-TV 

%<v%>%<tv%>:                                                 # ;


LEXICON N1

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 

LEXICON N2

%<n%>:%>ij                                                   Sufijos2 ; 
%<n%>:%>aaj                                                  Sufijos3 ; 

LEXICON N3

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos2 ; 
%<n%>%<pl%>:%>iibʼ                                           Sufijos1 ; 

LEXICON N4

%<n%>:                                                       Sufijos2 ; 
%<n%>:                                                       Sufijos1 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos2 ; 
%<n%>%<pl%>:%>ibʼ                                            Sufijos1 ; 

LEXICON N5

%<n%>:%>niik                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N6

%<n%>:%>xeel                                                 Sufijos2 ; 
%<n%>:                                                       Sufijos3 ; 

LEXICON N7

%<n%>:                                                       Sufijos3 ; 

LEXICON A

%<adj%>:                                                     # ; 
%<adj%>%<ints%>:%>laj                                        # ; 

LEXICON NUM1

%<num%>:                                                     # ; 

LEXICON NUM2

%<num%>:                                                     # ; 

LEXICON COG1

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>aabʼ                                   # ;

LEXICON COG2

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>iibʼ                                   # ;

LEXICON COG3

%<np%>%<cog%>:                                               # ;
%<np%>%<cog%>%<pl%>:%>ʼiibʼ                                  # ;

LEXICON ANTF

%<np%>%<ant%>%<f%>:                                          # ;

LEXICON ANTM

%<np%>%<ant%>%<m%>:                                          # ;

LEXICON TOP

%<np%>%<top%>:                                               # ;

LEXICON ADV1

%<adv%>:                                                     # ; 

LEXICON ADV2

%<adv%>%<itg%>:                                              # ; 

LEXICON DET1

%<det%>:                                                     # ; 

LEXICON DET2

%<det%>%<pl%>:                                               # ; 

LEXICON PRON2

%<prn%>%<itg%>:                                              # ; 



!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!

LEXICON Miscellaneous

ra%<dim%>:ra                 # ;
aj%<gen%>:aj                 # ;
no%<neg%>:no                 # ;
taʼ%<neg%>:taʼ               # ;

tijin%<aux%>:tijin           # ;

taq%<pl%>:taq                # ;

qaaj%<clf%>%<m%>:qaaj        # ;
chuch%<clf%>%<f%>:chuch      # ;

LEXICON Pronombres

neen:neen                    PRON2 ; ! "qué"

LEXICON Determinantes 

li:li                        DET1 ; ! "el"
ri:ri                        DET1 ; ! "el"
man:man                      DET1 ; ! "el"
man:maq                      DET2 ; ! "el"
yaj:yaj                      DET1 ; ! "el"
yaj:yaq                      DET2 ; ! "el"

LEXICON Numerales 

juun:juun                    NUM1 ; ! "uno"
kiibʼ:kiibʼ                  NUM2 ; ! "dos"
joobʼ:joobʼ                  NUM2 ; ! "cinco"

LEXICON Sustantivos 

ichaaj:ich                   N2 ; ! "hierba" [#]
íxim:íxim                    N1 ; ! "maíz" [#]
óoj:óoj                      N1 ; ! "aguacate" [#]
aabʼ:aabʼ                    N1 ; ! "hamaca" [#]
tzʼiʼ:tzʼiʼ                  N1 ; ! "perro" [#]
uuq:uuq                      N1 ; ! "corte" [#]
tikoʼn:tikoʼn                N1 ; ! "siembra" [#]
ikʼlaal:ikʼlaal              N1 ; ! "hijo (de mujer)" [#]
awaj:awaj                    N3 ; ! "animal" [#]
ajchaak:aj%>chaak            N4 ; ! "trabajador" [aj#]
ajbʼiix:aj%>bʼiix            N4 ; ! "cantante" [aj#]
iyoom:iyoom                  N1 ; ! "comadrona" [#]
bʼee:bʼee                    N1 ; ! "camino" [#]
jal:ja%{ː%}l                 N1 ; ! "mazorca" [#]
kʼum:kʼu%{ː%}m               N1 ; ! "ayote" [#]
tiʼniik:tiʼ                  N5 ; ! "carne" [#niik]
qʼuniik:qʼu                  N5 ; ! "chamarra" [#niik]
jiʼxeel:ji%{ː%}ʼ             N6 ; ! "yerno" [#xeel]
alibʼxeel:alibʼ              N6 ; ! "nuera" [#xeel]
kʼojolxeel:kʼojol            N6 ; ! "joven" [#xeel]
qajawxeel:qajaw              N6 ; ! "dueño" [#xeel] ! FIXME: qa- poss. ?
qʼunxeel:qʼun                N6 ; ! "hermano menor" [#xeel]

tinmit:tinmi%{ː%}t           N1 ; ! "pueblo" [#]

LEXICON Poseídos 

iij:iij                      N7 ; ! "espalda" [-#]
ichooch:ichooch              N7 ; ! "casa" [-#]

LEXICON Propios 

Chimel:Chimel                TOP ; 
Kʼaʼntun:Kʼaʼntun            TOP ; ! ""
Kʼotoxaq:Kʼotoxaq            TOP ; ! ""
Maqaljaʼ:Maqaljaʼ            TOP ; ! ""
Bʼoobʼlaj:Bʼoobʼlaj          TOP ; ! ""
Kʼisachálik:Kʼisachálik      TOP ; ! ""
Najtilja:Najtilja            TOP ; ! ""
Chʼipaq:Chʼipaq              TOP ; ! ""
Kʼixpaaj:Kʼixpaaj            TOP ; ! ""

Koʼr:Koʼr                    COG1 ; 
Tum:Tum                      COG2 ;
Aware:Aware                  COG3 ; 

Treʼs:Treʼs                  ANTF ; ! "Teresa" [#]
Terees:Terees                ANTF ; ! "Teresa" [#]
Tzʼúnun:Tzʼúnun              ANTF ; ! "" [#]

Liip:Liip                    ANTM ; ! "Felipe" [#]

LEXICON Adjetivos 

saq:saq                      A ; ! "blanco"
rex:rex                      A ; ! "verde"
keq:keq                      A ; ! "rojo"
qʼeq:qʼeq                    A ; ! "negro"
qʼan:qʼan                    A ; ! "amarillo"

chʼam:chʼam                  A ; ! "ácido"
kiʼ:kiʼ                      A ; ! "dulce"
kʼaa:kʼaa                    A ; ! "amargo"
tzaa:tzaa                    A ; ! "salado"
rii:rii                      A ; ! "picante"

nim:nim                      A ; ! "grande"
chaal:chaal                  A ; ! "pequeño"
nuuchʼ:nuuchʼ                A ; ! "pequeño"
maakʼ:maakʼ                  A ; ! "pequeño"

bʼot:bʼot                    A ; ! "blanco" < algodón
pool:pool                    A ; ! "blanco" < flor

LEXICON Adverbios 

lamaas:lamaas                ADV2 ; ! "dónde"

chweʼq:chweʼq                ADV1 ; ! "mañana"
íwir:íwir                    ADV1 ; ! "ayer"

LEXICON TV

:%> TVʼ ;

LEXICON IV

:%> IVʼ ;

LEXICON IVʼ

war:war                      V-IV ; ! "dormir"
wiʼn:wiʼn                    V-IV ; ! "comer"
oqʼ:oqʼ                      V-IV ; ! "llorar"

LEXICON TVʼ

chap:chap                    V-TV ; ! "agarrar"
kʼam:kʼam                    V-TV ; ! "recibir"
il:il                        V-TV ; ! "ver"
tukʼ:tukʼ                    V-TV ; ! "empujar"
kʼut:kʼut                    V-TV ; ! "enseñar"

